#include <select.hfa>
#include <thread.hfa>
#include <channel.hfa>

channel(long long int) C;

volatile bool done = false;
long long int globalTotal = 0;

thread Server1 {};
void main( Server1 & this ) {
    long long int c, i = 0, myTotal = 0;
    for( ;;i++ ) {
        waituntil( c << C ) { if ( c == -1 ) break; myTotal += c; }
    }
    __atomic_fetch_add( &globalTotal, myTotal, __ATOMIC_SEQ_CST );
}

size_t numtimes = 100000;
size_t numServers = 1;
int main( int argc, char * argv[] ) {
    if ( argc == 2 )
        numtimes = atoi( argv[1] );

    processor p[numServers];
    C{5};

    long long int total = 0;
    printf("start\n");
    {
        Server1 s[numServers];
        for( long long int j = 0; j < numtimes; j++ ) {
            waituntil( C << j ) { total += j; }
        }
        printf("waiting for empty channels\n");
        printf("sending sentinels\n");
        for ( i; numServers ) insert( C, -1 );
        printf("joining servers\n");
    }
    assert(get_count( C ) == 0);
    if ( total != globalTotal ) 
        printf("CHECKSUM MISMATCH!! Main thread got %lld, server sum is %lld\n", total, globalTotal);
    printf("done\n");
}