#include <vec/vec3.hfa>
#include <fstream.hfa>

int main(void) {
    vec3(float) v1 = {1.f,2.f,3.f};
    sout | "ctor(x,y):" | v1;

    vec3(float) v2 = v1;
    sout | "copy ctor:" | v2;

    v2 = (vec3(float)){3.f, 4.2f, -2.f};
    sout | "assignment:" | v2;

    v2 = v1;
    sout | "move assignment:" | v2;

    vec3(float) v3 = 0;
    sout | "zero-init:" | v3;

    v1 = 0;
    sout | "zero-assign:" | v1;

    vec3(float) v4 = {1.23f};
    sout | "fill-ctor:" | v4;

    v1 = (vec3(float)){1.23f, 3.43f, 0.000002f};
    sout | "?-?:" | (v1 - (vec3(float)){1.21f,3,1});

    v1 -= (vec3(float)){1.23f, 3.43f, 0.000002f};
    sout | "?-=?:" | v1;

    v1 = -v1;
    sout | "-?:" | v1;

    v1 = (vec3(float)){1.5f, 2.75f, -14.2f};
    sout | "?+?:" | (v1 + (vec3(float)){0.8f, -0.3f, 5});

    v1 += (vec3(float)){0.8f, -0.3f, 5};
    sout | "?+=?:" | v1;

    v1 = (vec3(float)){1.5f, 2.75f, 100.3f};
    sout | "v*s:" | v1 * 3.f;

    sout | "s*v:" | 3.f * v1;

    v1 *= 3;
    sout | "?*=?:" | v1;

    {
        vec3(float) u = {1, 2, 3};
        vec3(float) v = {2, 3, 4.5f};
        sout | "?*?(vec):" | (u * v);
        sout | "?*=?(vec):" | (u *= v);
    }

    v1 = (vec3(float)){2, -0.1f, 45};
    sout | "?/?:" | (v1 / 3.f);

    v1 /= 3.f;
    sout | "?/=?:" | v1;

    {
        vec3(float) u = {2, -0.1f, 3};
        vec3(float) v = {2, 3, 4.5f};
        sout | "?/?(vec):" | (u / v);
        sout | "?/=?(vec):" | (u /= v);
    }

    v1 = (vec3(float)){4,2,3};
    v2 = (vec3(float)){0,-3,2};
    sout | "dot_1:" | dot(v1, v2);

    v2 = (vec3(float)){1.3f, -2, 12.2f};
    sout | "dot_2:" | dot(v1, v2);

    v2 = cross((vec3(float)){3,-3,1},(vec3(float)){4,9,2});
    sout | "cross:" | v2;

    v1 = (vec3(float)){1,2,3};
    sout | "length:" | length(v1);

    sout | "length_squared:" | length_squared(v1);

    v2 = (vec3(float)){6, -3.2f, 1};
    sout | "distance:" | distance(v1, v2);

    sout | "normalize:" | normalize(v2);

    sout | "project:" | project((vec3(float)){5,6,0}, (vec3(float)){0, 0, 1});
    sout | "project_2:" | project((vec3(float)){5,6,3.2f}, (vec3(float)){10, 1, 4});

    v1 = (vec3(float)){5,6,2.333f};
    v2 = (vec3(float)){1,0,-13.5f};
    sout | "reflect:" | reflect(v1,v2);

    v2 = (vec3(float)){0,-1,2};
    sout | "refract:" | refract(normalize(v1),normalize(v2),1.f);
    sout | "refract:" | refract(normalize(v1),normalize(v2),1.f/1.33f);

    vec3(float) geometric_normal = {5,6,1};
    vec3(float) perturbed_normal = {4,5.5f,2};
    vec3(float) eyeline = {-1,0.002f,-1.0345f};
    sout | "faceforward_nochange:" | faceforward(perturbed_normal, eyeline, geometric_normal);

    eyeline = (vec3(float)){1,0.002f,-1.0345f};
    sout | "faceforward_flip:" | faceforward(perturbed_normal, eyeline, geometric_normal);
}
